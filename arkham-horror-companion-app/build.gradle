plugins {
  id "com.github.node-gradle.node" version "_"
  id "base"
}

yarn_build {
    inputs.files fileTree('public')
    inputs.files fileTree('src')

    inputs.file 'package.json'
    inputs.file 'yarn.lock'

    outputs.dir 'build'
}

String testsExecutedMarkerName = "${projectDir}/.tests.executed"
task test(type: YarnTask) {
    dependsOn assemble
    // force Jest test runner to execute tests once and finish the process instead of starting watch mode
    environment CI: 'true'
    args = ['run', 'test']
    inputs.files fileTree('src')
    inputs.file 'package.json'
    inputs.file 'yarn.lock'
    // allows easy triggering re-tests
    doLast {
        new File(testsExecutedMarkerName).text = 'delete this file to force re-execution JavaScript tests'
    }
    outputs.file testsExecutedMarkerName
}

clean {
    delete testsExecutedMarkerName
}

check.dependsOn test
assemble.dependsOn yarn_build